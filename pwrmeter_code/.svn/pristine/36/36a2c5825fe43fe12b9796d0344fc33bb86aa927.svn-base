// Copyright(c) 2016 Anseok Lee (anseok83_at_gmail_dot_com)
// MIT License
//
// THD (Temperature, humidity and dust density) Sensor
// https://github.com/anseok83/thdm

// Version 0.0.2 (2016-05-21)
// Initial code
// Version 0.0.3 (2016-06-24)
// Modify dust sensor data retrieving
// Adding Wifi manager
// Version 0.0.4 (2016-07-08)
// Standalone operation (with google chart) (todo)
// LED status monitoring (todo)


#include <FS.h>                   //this needs to be first, or it all crashes and burns...

// Windowing length
#define DUST_WINDOW_SIZE  60
#define TH_WINDOW_SIZE  5

// DHT-11 (Temperature and humidity sensor)
#include "DHT.h"
#define DHTPIN 4     // what digital pin we're connected to
#define DHTTYPE DHT11   // DHT 11

// ESP8266 (WiFi module) and socket.io
#include <ESP8266WiFi.h>
#include <ArduinoJson.h>
#include <DNSServer.h>
#include <ESP8266WebServer.h>
#include <WiFiManager.h>
#include <WiFiUdp.h>
#include "time_ntp.h"

// ntp timestamp
unsigned long ulSecs2000_timer = 0;

// storage for Measurements; keep some mem free; allocate remainder
#define KEEP_MEM_FREE 10240
//#define MEAS_SPAN_H 24
unsigned long ulMeasCount = 0;  // values already measured
unsigned long ulNoMeasValues = 0; // size of array
unsigned long ulMeasDelta_ms;   // distance to next meas time
//unsigned long ulNextMeas_ms;    // next meas time
unsigned long *pulTime;         // array for time points of measurements
float *pfTemp, *pfHum, *pfDust;          // array for temperature, humidity and dust measurements

unsigned long ulReqcount;       // how often has a valid page been requested
unsigned long ulReconncount;    // how often did we connect to WiFi

// Create an instance of the server on Port 80
WiFiServer server(80);


// WiFi Setup Reset PIN
#define TRIGGER_PIN 15

// Global variables for networking (Wifi, socket.io)
StaticJsonBuffer<200> jsonBuffer;

//const char* ssid     = "ANSNET";           // Wifi SSID
//const char* password = "poration33";      // Wifi password

char thd_interval[6] = "10";
int thd_interval_int = 0;

//flag for saving data
bool shouldSaveConfig = false;

char ssid[20] = "";
char password[20] = "";
//String ssid, password;

extern String RID;
extern String Rname;
extern String Rcontent;

// Reporting interval
bool isFirstUpdateDust = true;
bool isFirstUpdateTH = true;

unsigned long previousMillis = 0;
long interval = 5000;

// Sensor measurement interval
unsigned long previousDustMeas = 0;
long intervalDustMeas = 100;

unsigned long previousTHMeas = 0;
long intervalTHMeas = 2000;

// Global variables for sharp dust sensor
int dustPower = 5;
int dustMeasure = 0;

int samplingTime = 280;
int deltaTime = 40;
int sleepTime = 9680;

// Wifi manager
WiFiManager wifiManager;

float voMeasured = 0;
float voMeasured2 = 0, voMeasured1 = 0;
float calcVoltage = 0;
float dustDensity = 0;

int i;
float dustValue[DUST_WINDOW_SIZE];
int dustValueIndex = 0;
float dustValueSum, dustValueAvg = 0;
int dustRapidChange = 0;
int lastDustValueIndex = 0;

// Global variables for DHT-11
DHT dht(DHTPIN, DHTTYPE);

float tempValue[TH_WINDOW_SIZE];
float humidValue[TH_WINDOW_SIZE];
int tempValueIndex = 0;
float tempValueSum, tempValueAvg = 0;
float humidValueSum, humidValueAvg = 9;

//callback notifying us of the need to save config
void saveConfigCallback () {
  Serial.println("Should save config");
  shouldSaveConfig = true;
}

void setup() {
  ////////////////
  // Setup routine
  ////////////////

  // setup globals
  ulReqcount = 0;
  ulReconncount = 0;

  // setup serial port
  Serial.begin(115200);
  delay(1);

  pinMode(TRIGGER_PIN, INPUT);


  //read configuration from FS json
  Serial.println("mounting FS...");

  if (SPIFFS.begin()) {
    Serial.println("mounted file system");
    if (SPIFFS.exists("/config.json")) {
      //file exists, reading and loading
      Serial.println("reading config file");
      File configFile = SPIFFS.open("/config.json", "r");
      if (configFile) {
        Serial.println("opened config file");
        size_t size = configFile.size();
        // Allocate a buffer to store contents of the file.
        std::unique_ptr<char[]> buf(new char[size]);

        configFile.readBytes(buf.get(), size);
        DynamicJsonBuffer jsonBuffer;
        JsonObject& json = jsonBuffer.parseObject(buf.get());
        json.printTo(Serial);
        if (json.success()) {
          Serial.println("\nparsed json");

          strcpy(thd_interval, json["thd_interval"]);
          //            strcpy(blynk_token, json["blynk_token"]);

        } else {
          Serial.println("failed to load json config");
        }
      }
    }
  } else {
    Serial.println("failed to mount FS");
  }
  //end read

  // The extra parameters to be configured (can be either global or just in the setup)
  // After connecting, parameter.getValue() will get you the configured value
  // id/name placeholder/prompt default length
  //  WiFiManagerParameter custom_thd_server("server", "thd server", thd_server, 40);
  WiFiManagerParameter custom_thd_interval("interval", "thd interval [s]", thd_interval, 5);

  //WiFiManager
  //Local intialization. Once its business is done, there is no need to keep it around
  WiFiManager wifiManager;

  //wifiManager.resetSettings();
  //ESP.reset();

  //set config save notify callback
  wifiManager.setSaveConfigCallback(saveConfigCallback);

  //add all your parameters here
  //  wifiManager.addParameter(&custom_thd_server);
  wifiManager.addParameter(&custom_thd_interval);

  //fetches ssid and pass from eeprom and tries to connect
  //and goes into a blocking loop awaiting configuration
  if (!wifiManager.autoConnect("THDSensor") ) {
    Serial.println("failed to connect and hit timeout");
    delay(3000);
    //reset and try again, or maybe put it to deep sleep
    ESP.reset();
    delay(5000);
  }

  //if you get here you have connected to the WiFi
  Serial.println("connected...yeey :)");

  
  ///////////////////////////////
  // connect to NTP and get time
  ///////////////////////////////
  ulSecs2000_timer = getNTPTimestamp();
  Serial.print("Current Time UTC from NTP server: " );
  Serial.println(epoch_to_string(ulSecs2000_timer).c_str());

  ulSecs2000_timer -= millis() / 1000; // keep distance to millis() counter

  strcpy(thd_interval, custom_thd_interval.getValue());

  //save the custom parameters to FS
  if (shouldSaveConfig) {
    Serial.println("saving config");
    DynamicJsonBuffer jsonBuffer;
    JsonObject& json = jsonBuffer.createObject();
    json["thd_interval"] = thd_interval;

    File configFile = SPIFFS.open("/config.json", "w");
    if (!configFile) {
      Serial.println("failed to open config file for writing");
    }

    json.printTo(Serial);
    json.printTo(configFile);
    configFile.close();
    //end save
  }


  (WiFi.SSID()).toCharArray(ssid, 20);
  (WiFi.psk()).toCharArray(password, 20);

  thd_interval_int = atoi(thd_interval);


  // allocate ram for data storage
  uint32_t free = system_get_free_heap_size() - KEEP_MEM_FREE;
  ulNoMeasValues = free / (sizeof(float) * 3 + sizeof(unsigned long)); // humidity & temp & dust --> 3 + time
  pulTime = new unsigned long[ulNoMeasValues];
  pfTemp = new float[ulNoMeasValues];
  pfHum = new float[ulNoMeasValues];
  pfDust = new float[ulNoMeasValues];

  if (pulTime == NULL || pfTemp == NULL || pfHum == NULL || pfDust == NULL)
  {
    ulNoMeasValues = 0;
    Serial.println("Error in memory allocation!");
  }
  else
  {
    Serial.print("Allocated storage for ");
    Serial.print(ulNoMeasValues);
    Serial.println(" data points.");

    // [anseok] setting measurement interval
    //    float fMeasDelta_sec = MEAS_SPAN_H*3600./ulNoMeasValues;

    float fMeasDelta_sec = thd_interval_int;
    ulMeasDelta_ms = ( (unsigned long)(fMeasDelta_sec + 0.5) ) * 1000; // round to full sec
    Serial.print("Measurements will happen each ");
    Serial.print(ulMeasDelta_ms);
    Serial.println(" ms.");

//    ulNextMeas_ms = millis() + ulMeasDelta_ms;
  }
  
  // Setup IO pins
  pinMode(dustPower, OUTPUT);

  // DHT sensor initialization
  dht.begin();
}


/////////////////////////////////////
// make html table for measured data
/////////////////////////////////////
unsigned long MakeTable (WiFiClient *pclient, bool bStream)
{
  unsigned long ulLength = 0;

  // here we build a big table.
  // we cannot store this in a string as this will blow the memory
  // thus we count first to get the number of bytes and later on
  // we stream this out
  if (ulMeasCount == 0)
  {
    String sTable = "Noch keine Daten verf&uuml;gbar.<BR>";
    if (bStream)
    {
      pclient->print(sTable);
    }
    ulLength += sTable.length();
  }
  else
  {
    unsigned long ulEnd;
    if (ulMeasCount > ulNoMeasValues)
    {
      ulEnd = ulMeasCount - ulNoMeasValues;
    }
    else
    {
      ulEnd = 0;
    }

    String sTable;
    sTable = "<table style=\"width:100%\"><tr><th>Zeit / UTC</th><th>T &deg;C</th><th>Hum &#037;</th></tr>";
    sTable += "<style>table, th, td {border: 2px solid black; border-collapse: collapse;} th, td {padding: 5px;} th {text-align: left;}</style>";
    for (unsigned long li = ulMeasCount; li > ulEnd; li--)
    {
      unsigned long ulIndex = (li - 1) % ulNoMeasValues;
      sTable += "<tr><td>";
      sTable += epoch_to_string(pulTime[ulIndex]).c_str();
      sTable += "</td><td>";
      sTable += pfTemp[ulIndex];
      sTable += "</td><td>";
      sTable += pfHum[ulIndex];
      sTable += "</td></tr>";

      // play out in chunks of 1k
      if (sTable.length() > 1024)
      {
        if (bStream)
        {
          pclient->print(sTable);
          //pclient->write(sTable.c_str(),sTable.length());
        }
        ulLength += sTable.length();
        sTable = "";
      }
    }

    // remaining chunk
    sTable += "</table>";
    ulLength += sTable.length();
    if (bStream)
    {
      pclient->print(sTable);
      //pclient->write(sTable.c_str(),sTable.length());
    }
  }

  return (ulLength);
}


////////////////////////////////////////////////////
// make google chart object table for measured data
////////////////////////////////////////////////////
unsigned long MakeList (WiFiClient *pclient, bool bStream)
{
  unsigned long ulLength = 0;

  // here we build a big list.
  // we cannot store this in a string as this will blow the memory
  // thus we count first to get the number of bytes and later on
  // we stream this out
  if (ulMeasCount > 0)
  {
    unsigned long ulBegin;
    if (ulMeasCount > ulNoMeasValues)
    {
      ulBegin = ulMeasCount - ulNoMeasValues;
    }
    else
    {
      ulBegin = 0;
    }

    String sTable = "";
    for (unsigned long li = ulBegin; li < ulMeasCount; li++)
    {
      // result shall be ['18:24:08 - 21.5.2015',21.10,49.00],
      unsigned long ulIndex = li % ulNoMeasValues;
      sTable += "['";
      sTable += epoch_to_string(pulTime[ulIndex]).c_str();
      sTable += "',";
      sTable += pfTemp[ulIndex];
      sTable += ",";
      sTable += pfHum[ulIndex];
      sTable += "],\n";

      // play out in chunks of 1k
      if (sTable.length() > 1024)
      {
        if (bStream)
        {
          pclient->print(sTable);
          //pclient->write(sTable.c_str(),sTable.length());
        }
        ulLength += sTable.length();
        sTable = "";
      }
    }

    // remaining chunk
    if (bStream)
    {
      pclient->print(sTable);
      //pclient->write(sTable.c_str(),sTable.length());
    }
    ulLength += sTable.length();
  }

  return (ulLength);
}


//////////////////////////
// create HTTP 1.1 header
//////////////////////////
String MakeHTTPHeader(unsigned long ulLength)
{
  String sHeader;

  sHeader  = F("HTTP/1.1 200 OK\r\nContent-Length: ");
  sHeader += ulLength;
  sHeader += F("\r\nContent-Type: text/html\r\nConnection: close\r\n\r\n");

  return (sHeader);
}


////////////////////
// make html footer
////////////////////
String MakeHTTPFooter()
{
  String sResponse;

  sResponse  = F("<FONT SIZE=-2><BR>Aufrufz&auml;hler=");
  sResponse += ulReqcount;
  sResponse += F(" - Verbindungsz&auml;hler=");
  sResponse += ulReconncount;
  sResponse += F(" - Freies RAM=");
  sResponse += (uint32_t)system_get_free_heap_size();
  sResponse += F(" - Max. Datenpunkte=");
  sResponse += ulNoMeasValues;
  sResponse += F("<BR>Stefan Thesen 05/2015<BR></body></html>");

  return (sResponse);
}





void loop() {
  // Start measurement

  // is configuration portal requested?
  if ( digitalRead(TRIGGER_PIN) == HIGH ) {
    Serial.print("Reset ESP Board");
    delay(5000);
    wifiManager.resetSettings();
    ESP.reset();
  }

  // current time
  unsigned long currentMillis = millis();

  // Measurement interval (Dust sensor)
  if (currentMillis - previousDustMeas > intervalDustMeas)
  {
    previousDustMeas = currentMillis;

    // Dust sensor
    voMeasured1 = analogRead(dustMeasure); // read the dust value
    digitalWrite(dustPower, LOW); // power on the LED
    delayMicroseconds(samplingTime);
    voMeasured2 = analogRead(dustMeasure); // read the dust value

    voMeasured = voMeasured2 - voMeasured1;

    delayMicroseconds(deltaTime);
    digitalWrite(dustPower, HIGH); // turn the LED off
    delayMicroseconds(sleepTime);

    // 0 - 3.3V mapped to 0 - 1023 integer values
    calcVoltage = voMeasured * (3.3 / 1024);
    dustDensity = (0.172 * calcVoltage) * 1000;
    //dustDensity = ((0.172 * calcVoltage)-0.1)* 1000;  // dust density in ug/m^3

    // Dust sensor windowing & averaging
    dustValue[dustValueIndex] = dustDensity;
    lastDustValueIndex = dustValueIndex;
    dustValueIndex = (dustValueIndex + 1) % DUST_WINDOW_SIZE;

    // only for first update (intialization)
    if (isFirstUpdateDust==true)
    {
      for (int i=1;i<DUST_WINDOW_SIZE;i++)
      {
        dustValue[i] = dustValue[0];
      }

      isFirstUpdateDust = false;
    }

    //    dustValueSum = 0;
    //    for (i = 0; i < DUST_WINDOW_SIZE; i++) {
    //      dustValueSum += dustValue[i];
    //    }
    //
    //    dustValueAvg = (dustValueSum / DUST_WINDOW_SIZE);

    // Log (instantaneous)
    Serial.print("Raw Signal Value (0-1023): ");
    Serial.print(voMeasured);

    Serial.print(" - Voltage: ");
    Serial.print(calcVoltage);

    Serial.print(" - Dust Density [ug/m3]: ");
    Serial.println(dustDensity);

    Serial.print("voMeasured2(");
    Serial.print(voMeasured2);
    Serial.print(") - voMeasured1(");
    Serial.print(voMeasured1);
    Serial.print(" = ");
    Serial.println(voMeasured);

  }

  // Measurement interval (Dust sensor)
  if (currentMillis - previousTHMeas > intervalTHMeas)
  {
    previousTHMeas = currentMillis;

    // Temp. and humidity sensors
    // Reading temperature or humidity takes about 250 milliseconds!
    // Sensor readings may also be up to 2 seconds 'old' (its a very slow sensor)
    float h = dht.readHumidity();
    // Read temperature as Celsius (the default)
    float t = dht.readTemperature();
    // Read temperature as Fahrenheit (isFahrenheit = true)
    float f = dht.readTemperature(true);

    // Check if any reads failed and exit early (to try again).
    if (isnan(h) || isnan(t) || isnan(f)) {
      Serial.println("Failed to read from DHT sensor!");
      //return;
    }

    // Temp & humid sensor windowing & averaging
    tempValue[tempValueIndex] = t;
    humidValue[tempValueIndex] = h + 5;   // Adjustment (by anseok)
    tempValueIndex = (tempValueIndex + 1) % TH_WINDOW_SIZE;

    // only for first update (intialization)
    if (isFirstUpdateTH==true)
    {
      for (int i=1;i<TH_WINDOW_SIZE;i++)
      {
        tempValue[i] = tempValue[0];
        humidValue[i] = humidValue[0];
      }

      isFirstUpdateTH = false;
    }

    //    tempValueSum = 0;
    //    humidValueSum = 0;
    //    for (i = 0; i < TH_WINDOW_SIZE; i++) {
    //      tempValueSum += tempValue[i];
    //      humidValueSum += humidValue[i];
    //    }
    //
    //    // Averaging
    //    tempValueAvg = (tempValueSum / TH_WINDOW_SIZE);
    //    humidValueAvg = (humidValueSum / TH_WINDOW_SIZE);

    // Log (instantaneous)
    Serial.print("Temp. & Hum. sample OK: ");
    Serial.print(t);
    Serial.print(" *C, ");
    Serial.print(h);
    Serial.println(" %");
  }

  // Record measurement data

  if (currentMillis - previousMillis > interval)
  {
    previousMillis = currentMillis;

    // Averaging
    // dust density
    dustValueSum = 0;
    for (i = 0; i < DUST_WINDOW_SIZE; i++) {
      dustValueSum += dustValue[i];
    }

    dustValueAvg = (dustValueSum / DUST_WINDOW_SIZE);

    // temp & humidity
    tempValueSum = 0;
    humidValueSum = 0;
    for (i = 0; i < TH_WINDOW_SIZE; i++) {
      tempValueSum += tempValue[i];
      humidValueSum += humidValue[i];
    }

    tempValueAvg = (tempValueSum / TH_WINDOW_SIZE);
    humidValueAvg = (humidValueSum / TH_WINDOW_SIZE);

    // Stores measurement data
    pfHum[ulMeasCount % ulNoMeasValues] = humidValueAvg;
    pfTemp[ulMeasCount % ulNoMeasValues] = tempValueAvg;
    pfDust[ulMeasCount % ulNoMeasValues] = dustValueAvg;
    pulTime[ulMeasCount % ulNoMeasValues] = millis() / 1000 + ulSecs2000_timer;

    Serial.print("Logging Temperature: ");
    Serial.print(pfTemp[ulMeasCount % ulNoMeasValues]);
    Serial.print(" deg - Humidity: ");
    Serial.print(pfHum[ulMeasCount % ulNoMeasValues]);
    Serial.print("% - DustDensity: ");
    Serial.print(pfDust[ulMeasCount % ulNoMeasValues]);
    Serial.print("ug/m^3 - Time: ");
    Serial.println(pulTime[ulMeasCount % ulNoMeasValues]);

    ulMeasCount++;
  }

  // Check the connectivity
  // Wifi
  if (WiFi.status() != WL_CONNECTED)
  {
    Serial.println("Wifi connection disconnected!");
    WiFi.begin(ssid, password);

    while (WiFi.status() != WL_CONNECTED) {
      delay(500);
      Serial.print(".");
    }
    Serial.println("");
    Serial.println("WiFi connected");
    Serial.println("IP address: ");
    Serial.println(WiFi.localIP());
  }

  ///////////////////////////////////
  // Check if a client has connected
  ///////////////////////////////////
  WiFiClient client = server.available();
  if (!client)
  {
    // [anseok] slight delaying
    delay(10);
    return;
  }

  // Wait until the client sends some data
  Serial.println("new client");
  unsigned long ultimeout = millis()+250;
  while(!client.available() && (millis()<ultimeout) )
  {
    delay(1);
  }
  if(millis()>ultimeout) 
  { 
    Serial.println("client connection time-out!");
    return; 
  }
  
  /////////////////////////////////////
  // Read the first line of the request
  /////////////////////////////////////
  String sRequest = client.readStringUntil('\r');
  //Serial.println(sRequest);
  client.flush();
  
  // stop client, if request is empty
  if(sRequest=="")
  {
    Serial.println("empty request! - stopping client");
    client.stop();
    return;
  }
  
  // get path; end of path is either space or ?
  // Syntax is e.g. GET /?show=1234 HTTP/1.1
  String sPath="",sParam="", sCmd="";
  String sGetstart="GET ";
  int iStart,iEndSpace,iEndQuest;
  iStart = sRequest.indexOf(sGetstart);
  if (iStart>=0)
  {
    iStart+=+sGetstart.length();
    iEndSpace = sRequest.indexOf(" ",iStart);
    iEndQuest = sRequest.indexOf("?",iStart);
    
    // are there parameters?
    if(iEndSpace>0)
    {
      if(iEndQuest>0)
      {
        // there are parameters
        sPath  = sRequest.substring(iStart,iEndQuest);
        sParam = sRequest.substring(iEndQuest,iEndSpace);
      }
      else
      {
        // NO parameters
        sPath  = sRequest.substring(iStart,iEndSpace);
      }
    }
  }
    
  ///////////////////////////
  // format the html response
  ///////////////////////////
  String sResponse,sResponse2,sHeader;
  
  /////////////////////////////
  // format the html page for /
  /////////////////////////////
  if(sPath=="/") 
  {
    ulReqcount++;
    int iIndex= (ulMeasCount-1)%ulNoMeasValues;
    sResponse  = F("<html>\n<head>\n<title>WLAN Logger f&uuml;r Lufttemperatur und Feuchtigkeit</title>\n<script type=\"text/javascript\" src=\"https://www.google.com/jsapi?autoload={'modules':[{'name':'visualization','version':'1','packages':['gauge']}]}\"></script>\n<script type=\"text/javascript\">\nvar temp=");
    sResponse += pfTemp[iIndex];
    sResponse += F(",hum=");
    sResponse += pfHum[iIndex];
    sResponse += F(";\ngoogle.load('visualization', '1', {packages: ['gauge']});google.setOnLoadCallback(drawgaugetemp);google.setOnLoadCallback(drawgaugehum);\nvar gaugetempOptions = {min: -20, max: 50, yellowFrom: -20, yellowTo: 0,redFrom: 30, redTo: 50, minorTicks: 10, majorTicks: ['-20','-10','0','10','20','30','40','50']};\n");
    sResponse += F("var gaugehumOptions = {min: 0, max: 100, yellowFrom: 0, yellowTo: 25, redFrom: 75, redTo: 100, minorTicks: 10, majorTicks: ['0','10','20','30','40','50','60','70','80','90','100']};\nvar gaugetemp,gaugehum;\n\nfunction drawgaugetemp() {\ngaugetempData = new google.visualization.DataTable();\n");
    sResponse += F("gaugetempData.addColumn('number', '\260C');\ngaugetempData.addRows(1);\ngaugetempData.setCell(0, 0, temp);\ngaugetemp = new google.visualization.Gauge(document.getElementById('gaugetemp_div'));\ngaugetemp.draw(gaugetempData, gaugetempOptions);\n}\n\n");
    sResponse += F("function drawgaugehum() {\ngaugehumData = new google.visualization.DataTable();\ngaugehumData.addColumn('number', '%');\ngaugehumData.addRows(1);\ngaugehumData.setCell(0, 0, hum);\ngaugehum = new google.visualization.Gauge(document.getElementById('gaugehum_div'));\ngaugehum.draw(gaugehumData, gaugehumOptions);\n}\n");
    sResponse += F("</script>\n</head>\n<body>\n<font color=\"#000000\"><body bgcolor=\"#d0d0f0\"><meta name=\"viewport\" content=\"width=device-width, initial-scale=1.0, user-scalable=yes\"><h1>WLAN Logger f&uuml;r Lufttemperatur und Feuchtigkeit</h1>AMS2301 / DHT21 an ESP8266 <BR><BR><FONT SIZE=+1>Letzte Messung um ");
    sResponse += epoch_to_string(pulTime[iIndex]).c_str();
    sResponse += F(" UTC<BR>\n<div id=\"gaugetemp_div\" style=\"float:left; width:160px; height: 160px;\"></div> \n<div id=\"gaugehum_div\" style=\"float:left; width:160px; height: 160px;\"></div>\n<div style=\"clear:both;\"></div>");
    
    sResponse2 = F("<p>Temperatur & Feuchtigkeitsverlauf - Seiten laden l&auml;nger:<BR><a href=\"/grafik\">Grafik</a>     <a href=\"/tabelle\">Tabelle</a></p>");
    sResponse2 += MakeHTTPFooter().c_str();
    
    // Send the response to the client 
    client.print(MakeHTTPHeader(sResponse.length()+sResponse2.length()).c_str());
    client.print(sResponse);
    client.print(sResponse2);
  }
  else if(sPath=="/tabelle")
  ////////////////////////////////////
  // format the html page for /tabelle
  ////////////////////////////////////
  {
    ulReqcount++;
    unsigned long ulSizeList = MakeTable(&client,false); // get size of table first
    
    sResponse  = F("<html><head><title>WLAN Logger f&uuml;r Lufttemperatur und Feuchtigkeit</title></head><body>");
    sResponse += F("<font color=\"#000000\"><body bgcolor=\"#d0d0f0\">");
    sResponse += F("<meta name=\"viewport\" content=\"width=device-width, initial-scale=1.0, user-scalable=yes\">");
    sResponse += F("<h1>WLAN Logger f&uuml;r Lufttemperatur und Feuchtigkeit</h1>");
    sResponse += F("<FONT SIZE=+1>");
    sResponse += F("<a href=\"/\">Startseite</a><BR><BR>Letzte Messungen im Abstand von ");
    sResponse += ulMeasDelta_ms;
    sResponse += F("ms<BR>");
    // here the big table will follow later - but let us prepare the end first
      
    // part 2 of response - after the big table
    sResponse2 = MakeHTTPFooter().c_str();
    
    // Send the response to the client - delete strings after use to keep mem low
    client.print(MakeHTTPHeader(sResponse.length()+sResponse2.length()+ulSizeList).c_str()); 
    client.print(sResponse); sResponse="";
    MakeTable(&client,true);
    client.print(sResponse2);
  }
  else if(sPath=="/grafik")
  ///////////////////////////////////
  // format the html page for /grafik
  ///////////////////////////////////
  {
    ulReqcount++;
    unsigned long ulSizeList = MakeList(&client,false); // get size of list first

    sResponse  = F("<html>\n<head>\n<title>WLAN Logger f&uuml;r Lufttemperatur und Feuchtigkeit</title>\n<script type=\"text/javascript\" src=\"https://www.google.com/jsapi?autoload={'modules':[{'name':'visualization','version':'1','packages':['corechart']}]}\"></script>\n");
    sResponse += F("<script type=\"text/javascript\"> google.setOnLoadCallback(drawChart);\nfunction drawChart() {var data = google.visualization.arrayToDataTable([\n['Zeit / UTC', 'Temperatur', 'Feuchtigkeit'],\n");    
    // here the big list will follow later - but let us prepare the end first
      
    // part 2 of response - after the big list
    sResponse2  = F("]);\nvar options = {title: 'Verlauf',vAxes:{0:{viewWindowMode:'explicit',gridlines:{color:'black'},format:\"##.##\260C\"},1: {gridlines:{color:'transparent'},format:\"##,##%\"},},series:{0:{targetAxisIndex:0},1:{targetAxisIndex:1},},curveType:'none',legend:{ position: 'bottom'}};");
    sResponse2 += F("var chart = new google.visualization.LineChart(document.getElementById('curve_chart'));chart.draw(data, options);}\n</script>\n</head>\n");
    sResponse2 += F("<body>\n<font color=\"#000000\"><body bgcolor=\"#d0d0f0\"><meta name=\"viewport\" content=\"width=device-width, initial-scale=1.0, user-scalable=yes\"><h1>WLAN Logger f&uuml;r Lufttemperatur und Feuchtigkeit</h1><a href=\"/\">Startseite</a><BR>");
    sResponse2 += F("<BR>\n<div id=\"curve_chart\" style=\"width: 600px; height: 400px\"></div>");
    sResponse2 += MakeHTTPFooter().c_str();
    
    // Send the response to the client - delete strings after use to keep mem low
    client.print(MakeHTTPHeader(sResponse.length()+sResponse2.length()+ulSizeList).c_str()); 
    client.print(sResponse); sResponse="";
    MakeList(&client,true);
    client.print(sResponse2);
  }
  else
  ////////////////////////////
  // 404 for non-matching path
  ////////////////////////////
  {
    sResponse="<html><head><title>404 Not Found</title></head><body><h1>Not Found</h1><p>The requested URL was not found on this server.</p></body></html>";
    
    sHeader  = F("HTTP/1.1 404 Not found\r\nContent-Length: ");
    sHeader += sResponse.length();
    sHeader += F("\r\nContent-Type: text/html\r\nConnection: close\r\n\r\n");
    
    // Send the response to the client
    client.print(sHeader);
    client.print(sResponse);
  }
  
  // and stop the client
  client.stop();
  Serial.println("Client disconnected");

}


